WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "<!--" ~ (!"-->" ~ ANY)* ~ "-->" }

file = { SOI ~ doctype ~ html ~ EOI }
    doctype = _{ "<!DOCTYPE" ~ (!">" ~ ANY)* ~ ">" }
    html    = _{ h_start ~ head ~ body ~ script* ~ h_end } 
        h_start = _{ "<html" ~ (!">" ~ ANY)* ~ ">" }
        head    = _{ "<head>" ~ (!"</head>" ~ ANY)* ~ "</head>" }
        body    = _{ "<body>" ~ (!"</body>" ~ div )* ~ "</body>" }
            div = _{ "<div" ~ (!">" ~ ANY)* ~ ">" ~ (!"</div>" ~ (div | target))* ~ "</div>" }
        script  = _{ "<script" ~ (!">" ~ ANY)* ~ ">" ~ (!"</script>" ~ ANY)* ~ "</script>" }
        h_end   = _{ "</html>" }

target = _{ error_codes | api_keys | req_resp | ANY }

error_codes = {
    "<h5>" ~
    "<a id=\"protocol_error_codes\"" ~ (!">" ~ ANY)* ~ ">" ~ (!"</a>" ~ ANY)* ~ "</a>" ~
    "</h5>" ~
    any_but_table ~
    table
}

api_keys = {
    "<h5>" ~
    "<a id=\"protocol_api_keys\"" ~ (!">" ~ ANY)* ~ ">" ~ (!"</a>" ~ ANY)* ~ "</a>" ~
    "</h5>" ~
    any_but_table ~
    table
}

req_resp = {
    "<h5>" ~
    "<a name=" ~ (!">" ~ ANY)* ~ ">" ~ (!"</a>" ~ ANY)* ~ "</a>" ~
    "</h5>" ~
    any_but_pre ~
    spec
}

any_but_table = _{ (!"<table class=\"data-table\">" ~ ANY )* }
any_but_pre   = _{ (!"<pre>" ~ ANY )* }

table = { tstart ~ thead ~ tinner ~ tend }
    tstart = _{ "<table class=\"data-table\">" ~ "<tbody>" }
    thead  = _{ "<tr>" ~ (!"</tr>" ~ th)* ~ "</tr>" }
        th = _{ "<th>" ~ (!"</th>" ~ ANY)* ~ "</th>" }
    tinner = _{ (!"</table>" ~ tr)* }
        tr =  { "<tr>" ~ (!"</tr>" ~ td)* ~ "</tr>" }
        td =  { "<td>" ~ (a | content) ~ "</td>" }
         a =  { "<a" ~ (!">" ~ ANY)* ~ ">" ~ content ~ "</a>" }
    tend   = _{ "</table>" }

spec = _{ pre ~ table }
    pre = _{ "<pre>" ~ bnf ~ "</pre>" }
    bnf =  { (!"</pre>" ~ ANY)* }

content = { (!"</" ~ char)* }

char = {
    ASCII_ALPHANUMERIC |
    "_" | "-" | "." | "," | "'" | ":" | "/" | "|" |
    "(" | ")" | "[" | "]" | "{" | "}"
}
