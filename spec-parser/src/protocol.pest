WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "<!--" ~ (!"-->" ~ ANY)* ~ "-->" }

file = { SOI ~ doctype ~ html ~ EOI }
    doctype = _{ "<!DOCTYPE" ~ (!">" ~ ANY)* ~ ">" }
    html    = _{ h_start ~ head ~ body ~ script* ~ h_end } 
        h_start = _{ "<html" ~ (!">" ~ ANY)* ~ ">" }
        head    = _{ "<head>" ~ (!"</head>" ~ ANY)* ~ "</head>" }
        body    = _{ "<body>" ~
                     (!("<h5>" ~ id_error_codes) ~ ANY)* ~
                         error_codes ~
                     (!("<h5>" ~ id_api_keys) ~ ANY)* ~
                         api_keys ~
                     ((!("<h5>" ~ named_tag_a) ~ ANY)* ~
                         req_resp)+ ~
                     (!"</body>" ~ ANY)* ~
                     "</body>" }
        script  = _{ "<script" ~ (!">" ~ ANY)* ~ ">" ~ (!"</script>" ~ ANY)* ~ "</script>" }
        h_end   = _{ "</html>" }

error_codes = { "<h5>" ~ id_error_codes ~ "</h5>" ~ any_but_table ~ table }
    id_error_codes = _{ "<a id=\"protocol_error_codes\"" ~ end_tag_a }

api_keys = { "<h5>" ~ id_api_keys ~ "</h5>" ~ any_but_table ~ table }
    id_api_keys = _{ "<a id=\"protocol_api_keys\"" ~ end_tag_a }

req_resp = { "<h5>" ~ named_tag_a ~ "</h5>" ~ spec_raw }
    named_tag_a = _{ "<a name=" ~ end_tag_a }
    spec_raw    =  { (!("<h5>"|"<h4>") ~ ANY)* }

spec = { ( any_but_pre ~ "<pre>" ~ content ~ "</pre>" ~ table )+ ~ ANY* }

any_but_table = _{ (!"<table class=\"data-table\">" ~ ANY)* }
any_but_pre   = _{ (!"<pre>" ~ ANY)* }
end_tag_a     = _{ (!">" ~ ANY)* ~ ">" ~ (!"</a>" ~ ANY)* ~ "</a>" }

table = { tstart ~ thead ~ tinner ~ tend }
    tstart = _{ "<table class=\"data-table\">" ~ "<tbody>" }
    thead  = _{ "<tr>" ~ (!"</tr>" ~ th)* ~ "</tr>" }
        th = _{ "<th>" ~ (!"</th>" ~ ANY)* ~ "</th>" }
    tinner = _{ (!"</table>" ~ tr)* }
        tr =  { "<tr>" ~ (!"</tr>" ~ td)* ~ "</tr>" }
        td =  { "<td>" ~ (a | content) ~ "</td>" }
         a =  { "<a" ~ (!">" ~ ("=" | ANY))* ~ ">" ~ content ~ "</a>" }
    tend   = _{ "</table>" }

content = { (!end_tags ~ char)* }
    end_tags = _{ "</pre>" | "</td>" | "</a>" }

char = {
    ASCII_ALPHANUMERIC |
    "_" | "-" | "." | "," | "'" | ":" | "/" | "|" | "=" |
    "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">"
}
