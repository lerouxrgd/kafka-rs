WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "<!--" ~ (!"-->" ~ ANY)* ~ "-->" }

char = {
    ASCII_ALPHANUMERIC |
    "_" | "-" | "." | "," | "'" | ":" | "/" | "|" |
    "(" | ")" | "[" | "]" | "{" | "}"
}
content = { char* }

doctype = _{ "<!DOCTYPE" ~ (!">" ~ ANY)* ~ ">" }

head = _{ "<head>" ~ (!"</head>" ~ ANY)* ~ "</head>" }

script = _{ "<script" ~ (!">" ~ ANY)* ~ ">" ~ (!"</script>" ~ ANY)* ~ "</script>" }

body = _{ "<body>" ~ (!"</body>" ~ div )* ~ "</body>" }

div = _{ "<div" ~ (!">" ~ ANY)* ~ ">" ~ (!"</div>" ~ (div | el))* ~ "</div>" }

el = _{ error_codes | ANY }

// h5 = { "<h5>" ~ (!"</h5>" ~ ANY)* ~ "</h5>" }

error_codes = {
    "<h5>" ~
    "<a id=\"protocol_error_codes\"" ~
    (!">" ~ ANY)* ~ ">" ~ (!"</a>" ~ ANY)* ~
    "</a>" ~
    "</h5>" ~
    any_but_table ~
    table
}

any_but_table = _{ (!"<table class=\"data-table\">" ~ ANY )* }

table = { tstart ~ thead ~ tinner ~ tend }
    tstart = _{ "<table class=\"data-table\">" ~ "<tbody>" }
    thead  = _{ "<tr>" ~ (!"</tr>" ~ th)* ~ "</tr>" }
        th = _{ "<th>" ~ (!"</th>" ~ ANY)* ~ "</th>" }
    tinner =  { (!"</table>" ~ tr)* }
        tr =  { "<tr>" ~ (!"</tr>" ~ td)* ~ "</tr>" }
        td =  { "<td>" ~ content ~ "</td>" }
    tend   = _{ "</table>" }

html = _{
   "<html" ~ (!">" ~ ANY)* ~ ">" ~
   head ~
   body ~
   script* ~
   "</html>"
}

file = {
    SOI ~
    doctype ~
    html ~
    EOI
}